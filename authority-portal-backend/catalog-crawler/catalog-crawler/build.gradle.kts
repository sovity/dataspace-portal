/*
 * Data Space Portal
 * Copyright (C) 2025 sovity GmbH
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

import com.squareup.javapoet.AnnotationSpec
import com.squareup.javapoet.FieldSpec
import com.squareup.javapoet.JavaFile
import com.squareup.javapoet.TypeName
import com.squareup.javapoet.TypeSpec
import javax.annotation.processing.Generated
import javax.lang.model.element.Modifier.FINAL
import javax.lang.model.element.Modifier.PUBLIC
import javax.lang.model.element.Modifier.STATIC
import java.lang.String as JavaString

plugins {
    `java-library`
    `maven-publish`
}

buildscript {
    dependencies {
        classpath(libs.squareup.javapoet)
        classpath(libs.lombok)
    }
}

dependencies {
    annotationProcessor(libs.lombok)
    compileOnly(libs.lombok)

    api(libs.edc.apiCore)
    api(libs.edc.connectorCore)
    api(libs.edc.contractDefinitionApi)
    api(libs.edc.controlApiConfiguration)
    api(libs.edc.controlPlaneApi)
    api(libs.edc.controlPlaneApiClient)
    api(libs.edc.controlPlaneContract)
    api(libs.edc.controlPlaneCore)
    api(libs.edc.dataPlaneSelectorCore)
    api(libs.edc.dataPlaneSelectorControlApi)
    api(libs.edc.dataPlaneSignalingClient)
    api(libs.edc.dsp)
    api(libs.edc.dspApiConfiguration)
    api(libs.edc.dspHttpCore)
    api(libs.edc.http)
    api(libs.edc.jsonLd)
    api(libs.edc.managementApiConfiguration)
    api(libs.edc.policyDefinitionApi)
    api(libs.edc.secretsApi)
    api(libs.edc.tokenCore)
    api(libs.edc.transferProcessApi)
    api(libs.edc.keysLib)
    api(libs.edc.boot)
    api(libs.edc.bootLib)
    api(libs.edc.oauth2Core)

    implementation(libs.quartz.quartz)
    implementation(libs.commons.lang3)
    implementation(libs.quarkus.jooq)
    implementation(libs.postgresql)

    api(libs.sovity.edc.jsonLdLib)
    api(libs.sovity.edc.mappersLib)
    api(project(":authority-portal-db"))

    // Logging
    api(libs.log4j.core)
    api(libs.log4j.jul)
    api(libs.log4j.layoutTemplateJson)

    testAnnotationProcessor(libs.lombok)
    testCompileOnly(libs.lombok)
    testImplementation(libs.assertj.core)
    testImplementation(libs.mockito.core)
    testImplementation(libs.restAssured.restAssured)
    testImplementation(libs.testcontainers.testcontainers)
    testImplementation(libs.flyway.core)
    testImplementation(libs.testcontainers.junitJupiter)
    testImplementation(libs.testcontainers.postgresql)
    testImplementation(libs.junit.api)
    testImplementation(libs.jsonAssert)
    testRuntimeOnly(libs.junit.engine)
}

tasks.getByName<Test>("test") {
    useJUnitPlatform()
    maxParallelForks = 1
}

val generateVersionFiles by tasks.creating {
    val targetFolder = "generated/sources/gradle/main/java"

    group = "sovity"
    description = "Generates the versions from libs.versions.toml into CrawlerGradleVersions"

    outputs.cacheIf { true }
    inputs.file(project.rootDir.resolve("gradle/libs.versions.toml"))
    outputs.dir(project.layout.buildDirectory.file(targetFolder))

    doLast {
        generateJvmVersions(project.layout.buildDirectory.file(targetFolder))
    }
    sourceSets["main"].java.srcDir(project.layout.buildDirectory.file(targetFolder))
}

tasks.getByName("compileJava") {
    dependsOn(generateVersionFiles)
}

tasks.getByName("compileKotlin") {
    dependsOn(generateVersionFiles)
}

fun generateJvmVersions(target: Provider<RegularFile>) {
    val versionsClass = TypeSpec.classBuilder("CrawlerGradleVersions")
        .addAnnotation(
            AnnotationSpec.builder(Generated::class.java)
                .addMember("value", "\$S", "Generated by the gradle ${project.path}:${generateVersionFiles} task")
                .build()
        )
        .addModifiers(PUBLIC, FINAL)
        .addField(
            FieldSpec.builder(TypeName.get(JavaString::class.java), "POSTGRES_IMAGE_TAG")
                .initializer("\$S", libs.versions.postgresDbImage.get())
                .addModifiers(PUBLIC, STATIC, FINAL)
                .build()
        )
        .build()
    val packageName = "de.sovity.edc.ce.versions"
    val javaFile = JavaFile.builder(packageName, versionsClass).build()

    javaFile.writeTo(file(target))
}

group = libs.versions.sovityEdcGroup.get()

publishing {
    publications {
        create<MavenPublication>(project.name) {
            from(components["java"])
        }
    }
}
